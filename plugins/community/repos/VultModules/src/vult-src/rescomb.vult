/*
Copyright (c) 2017 Leonardo Laguna Ruiz (modlfo@gmail.com), All rights reserved.

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1.- Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
2.- Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.
3.- Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.
4.- Commercial use requires explicit permission of the author.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/


fun delay(x:real, cv:real) : real {
   mem buffer : array(real, 3000);
   mem write_pos = (write_pos+1) % size(buffer);
   _ = set(buffer, write_pos, x);
   // Gets the position in the buffer to read
   val r_size = real(size(buffer));
   val r_index  = (real(write_pos) - Util.cvToperiodUnit(cv) * Util.getSampleRate()) % r_size;
   r_index = if r_index < 0.0 then r_size + r_index else r_index;
   val t1 = int(floor(r_index)) % size(buffer);
   val t2 = (t1 + 1) % size(buffer);
   // Gets the decimal part of the position
   val decimal  = r_index - real(t1);
   // Reads the values in the buffer
   val x1 = get(buffer,t1);
   val x2 = get(buffer,t2);
   // Interpolates the value
   val ret = (x2-x1) * decimal + x1;
   return ret;
}

fun toneCurve(tone) @[table(min=-1.2,max=1.2,size=64)] {
   val sign = if tone < 0.0 then -1.0 else 1.0;
   return sign * sqrt(abs(tone));
}

fun do(in, cv, tone, res) {
   mem output, stone;
   if(Util.change(tone)){
      stone = toneCurve(tone);
   }
   val feedback = Util.dcblock(output * res);
   val saturated_input = Util.stanh(in + feedback);
   output = stone * delay(saturated_input, cv) + in;
   return Util.stanh(output);
}
