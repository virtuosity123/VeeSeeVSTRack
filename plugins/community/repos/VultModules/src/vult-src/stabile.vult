/*
Copyright (c) 2017 Leonardo Laguna Ruiz (modlfo@gmail.com), All rights reserved.

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1.- Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
2.- Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.
3.- Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.
4.- Commercial use requires explicit permission of the author.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/


fun calc_g(cv, fs) {
   val pitch = Util.cvToPitch(cv);
   val f = 8.175798915643707 * exp(0.057762265046662105 * pitch);
   val wd = 2.0 * pi() * f;
	val T  = 1.0 / fs;
	val wa = (2.0 / T) * tan(wd * T / 2.0);
	val g  = wa * T / 2.0;
   return g;
}

fun calc_g_44100(cv) @[table(size=128, min=0.0, max=1.0)]{
   return calc_g(cv, 44100.0);
}

fun calc_g_48000(cv) @[table(size=128, min=0.0, max=1.0)]{
   return calc_g(cv, 48000.0);
}

fun calc_g_88200(cv) @[table(size=128, min=0.0, max=1.0)]{
   return calc_g(cv, 88200.0);
}

fun calc_g_96000(cv) @[table(size=128, min=0.0, max=1.0)]{
   return calc_g(cv, 96000.0);
}

fun calc_g_176400(cv) @[table(size=128, min=0.0, max=1.0)]{
   return calc_g(cv, 176400.0);
}

fun calc_g_192000(cv) @[table(size=128, min=0.0, max=1.0)]{
   return calc_g(cv, 192000.0);
}

fun tune(cv) {
   val fs = Util.getSampleRate ();
   val g = 0.0;
   if (fs == 44100.0)
      g = calc_g_44100(cv);
   else if (fs == 48000.0)
      g = calc_g_48000(cv);
   else if (fs == 88200.0)
      g = calc_g_88200(cv);
   else if (fs == 96000.0)
      g = calc_g_96000(cv);
   else if (fs == 176400.0)
      g = calc_g_176400(cv);
   else if (fs == 192000.0)
      g = calc_g_192000(cv);
   else
      g = calc_g(cv, Util.getSampleRate());
   return g;
}

fun process(x, cv, q, semblance) {
   mem z1, z2;
   mem g,inv_den, R;
   q = q + 0.5;
   if(Util.change(cv) || Util.change(q)) {
      g = tune(cv);
      R = 1.0 / (2.0 * (q + eps()));
      inv_den = 1.0/(1.0 + 2.0 * R * g + g * g);
   }

   val high  = (x - (2.0 * R + g) * z1 - z2) * inv_den;
	val band  = g * high + z1;
	val low   = g * band + z2;

	z1 = g * high + band;
	z2 = g * band + low;

   val sem = Util.fadeA(semblance) * low + Util.fadeB(semblance) * high;
   return low, band, high, sem;
}
and default() @[init] {
   g = calc_g(0.0, Util.getSampleRate());
   R = 1.0 / (2.0 * 0.5);
   inv_den = 1.0 / (1.0 + g * g);
 }